apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

def release_versionName = "1.0.0"
android {
    compileSdkVersion compile_sdk_version.toInteger()


    defaultConfig {
        minSdkVersion min_sdk
        targetSdkVersion target_sdk
        vectorDrawables.useSupportLibrary true
        versionCode 1
        versionName release_versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:' + support_version
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.drojian.workout'
            artifactId 'RoundProgressBar'
            version = release_versionName
            artifact(androidSourcesJar)
            artifact("$buildDir/outputs/aar/$project.name-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each {
                    if (it.group != null && it.name != null && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven{
            url = 'http://maven.raocc.net:8081/repository/workout-release/'
            credentials {
                username = 'workout-coder'
                password = workout_maven_psw
            }
        }
    }
}